apply plugin: 'java'
apply plugin: 'maven'

group = 'xanindorf'
version = '1.0'
description = 'Snaykuu'

defaultTasks 'run'

task run(type: JavaExec) {
	main = 'Main'
	classpath = sourceSets.main.runtimeClasspath
}

project.ext {
	jvm_version = '1.8'
	jarName = 'snaykuu.jar'
	bin = 'classes'
	src = 'src'
	bot = 'bot'
	img = 'img'
	doc = 'doc'
	encoding = 'UTF-8'
}

dependencies {
}

repositories {
	maven { url "http://jcenter.bintray.com" }
}

compileJava {
	sourceCompatibility = jvm_version
	targetCompatibility = jvm_version
	options.incremental = true
	options.encoding = encoding
}

sourceSets {
	main {
		java {
			srcDirs src
			srcDirs bot
		}
		resources {
			srcDirs img
		}
	}
}

jar {
	archiveName jarName
	destinationDir = file('.')
	from(img) {
		into(img)
	}
	manifest {
		attributes 'Main-Class': 'Main'
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	description 'Create a Jar file for the application, including source'
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	javadoc.failOnError = false
	from javadoc.destinationDir
}

task bot(type: Copy, dependsOn: classes) {
	from 'build/classes/java/main/bot'
	include "*.class"
	into bot
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

task wrapper(type: Wrapper) {
	description = 'Generates gradlew[.bat] scripts for faster execution'
	gradleVersion = '4.7'
}
